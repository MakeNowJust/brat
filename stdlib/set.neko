@brat = $loader.loadmodule("core", $loader);

set = @brat.base_object.new();
set.squish(@brat.enumerable);
set.new = $varargs(function(args) {
	var arglen = $asize(args);
	if(arglen == 1) {
		if($typeof(args[0]) == $tarray)
			args = args[0];
		else if($typeof(args[0]) == $tobject && $objget(args[0], $hash("@neko_array")) != null) {
			args = args[0].@neko_array();
			arglen = $asize(args);
		}
	}
	else if(arglen == 0)
		args = null;

	var @internal_hash = @brat.base_hash.new();

	if(args != null) {
		var i = 0;
		while(i < arglen) {
			@internal_hash.set(args[i], null);
			i = i + 1;
		}
	}
	var new_set = @brat.new_brat(this);
	new_set.@internal_hash = @internal_hash;
	new_set;
})

set.@to_s = function() {
	if(this.@internal_hash != null)
		this.@internal_hash.keys().@to_s();
	else
		"[]";
}

set.@equal@equal = function(rhs) {
	if($pcompare(this, rhs) == 0)
		@brat.base_object.@@true;
	else if($objget(rhs, $hash("@internal_hash")) == null)
		@brat.base_object.@@false;
	else if(this.length() != rhs.length())
		@brat.base_object.@@false;
	else {
		var flag = true;
		var keys = this.@internal_hash.keys().@neko_array();
		var stop = $asize(keys);
		var index = 0;

		while(index < stop && flag) {
			if(rhs.include@question(keys[index]) == @brat.base_object.@@false)
				flag = false;

			index = index + 1;
		}

		if(flag)
			@brat.base_object.@@true;
		else
			@brat.base_object.@@false;
	}
}

set.add = function(item) {
	this.@internal_hash.set(item, null);
	this;
}

set.@less@less = set.add;

set.delete = function(item) {
	this.@internal_hash.delete(item);
	this;
}

set.each = function(block) {
	this.@internal_hash.each@underkey(block);
}

set.include@question = function(item) {
	this.@internal_hash.key@question(item);
}

set.to@underarray = function() {
	this.@internal_hash.keys();
}

set.clear = function() {
	this.@internal_hash = @brat.base_hash.new();
	this;
}

set.empty@question = function() {
	this.@internal_hash.empty@question();
}

set.length = function() {
	this.@internal_hash.length();
}

$exports.set = set;
