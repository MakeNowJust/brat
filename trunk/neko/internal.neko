var base_object = $new(null);

base_object.new = $varargs(function(args) {
	var arg_hash;
	if($asize(args) > 0) {
		arg_hash = args[0];
	}
	else
	{
		arg_hash = $hnew(0);
	}
	return new_brat(this, arg_hash);
})

//Create a new object with either parent_brat or base_object
//as the parent class.
new_brat = function(parent_brat, arg_hash) {
	var brat = $new(null);

	if(parent_brat == null) {
		$objsetproto(brat, base_object);
	}
	else 
	{
		$objsetproto(brat, parent_brat);
	}


	if(arg_hash != null && $hsize(arg_hash) > 0) {
		var set_f = function(k,v) {
			$objset(this, $hash(k), v);
		}

		$hiter(arg_hash, set_f);
	}

	return brat;
}


base_object.clone = function() {
	var brat = $new(this);
	$objsetproto(brat, $objgetproto(this));
	brat;
}

base_object.my = function() {
	return this;
}
base_object.p = $varargs(function(args) {
	var i = 0;
	var len = $asize(args);
	while i < len {
		if(has_field(args[i], "to@unders")) {
			args[i] = args[i].to@unders();
		};
		i += 1;
	};
	$call($print, null, args);
	$print("\n");
})

var @false_value = new_brat(null, null);
@false_value.to@unders = function() { "false" };
var @true_value = new_brat(null, null);
@true_value.to@unders = function() { "true" };
var @null_value = new_brat(null, null);
@null_value.to@unders = function() { "null" };
base_object.@false = function() { @false_value }
base_object.@true = function() { @true_value }
base_object.@null = function() { @null_value}

base_object.@equal = function(rhs) {
	var lhs = this;
	if(has_field(rhs, "@value") && has_field(lhs, "@value")) {
		rhs = rhs.@value;
		lhs = lhs.@value;
	}

	if(lhs == rhs)
		this.@true();
	else
		this.@false();
}

var istrue = function(arg) {
	if(arg == base_object.@null() || arg == base_object.@false()) {
		false;
	}
	else {
		true;
	}
}

base_object.@true@question = $varargs(function(args) {
	var first;
	if($typeof(args[0]) == $tfunction) {
		first = $exports.invoke(args[0], $array(), this);
	}
	else {
		first = args[0]
	}

	if($asize(args) == 0) {
		if(istrue(this))
			base_object.@true();
		else
			base_object.@false();
	}
	else if($asize(args) == 1) {
		if(istrue(first))
			base_object.@true();
		else
			base_object.@false();
	}
	else if($asize(args) == 2) {
		if(istrue(first)) {
			if($typeof(args[1]) == $tfunction) {
				$exports.invoke(args[1], $array(), this);
			}
			else {
				args[1];
			}
		}
		else {
			first;
		}
	}
	else if($asize(args) == 3) {
		if(istrue(first)) {
			if($typeof(args[1]) == $tfunction) {
				$exports.invoke(args[1], $array(), this);
			}
			else {
				args[1];
			}
		}
		else {
			if($typeof(args[2]) == $tfunction) {
				$exports.invoke(args[2], $array(), this);
			}
			else {
				args[2];
			}
		}
	}
	else {
		$throw("Wrong number of arguments. Should be 0 - 3 arguments.");
	}

})

base_object.@false@question = $varargs(function(args) {
	var first;
	if($typeof(args[0]) == $tfunction) {
		first = $exports.invoke(args[0], $array(), this);
	}
	else {
		first = args[0]
	}
	if($asize(args) == 0) {
		if($not(istrue(this)))
			base_object.@true();
		else
			base_object.@false();
	}
	else if($asize(args) == 1) {
		if($not(istrue(first)))
			base_object.@true();
		else
			base_object.@false();
	}
	else if($asize(args) == 2) {
		if($not(istrue(first))) {
			if($typeof(args[1]) == $tfunction) {
				$exports.invoke(args[1], $array(), this);
			}
			else {
				args[1];
			}
		}
		else {
			first;
		}
	}
	else if($asize(args) == 3) {
		if($not(istrue(first))) {
			if($typeof(args[1]) == $tfunction) {
				$exports.invoke(args[1], $array(), this);
			}
			else {
				args[1];
			}
		}
		else {
			if($typeof(args[2]) == $tfunction) {
				$exports.invoke(args[2], $array(), this);
			}
			else {
				args[2];
			}

		}
	}
	else {
		$throw("Wrong number of arguments. Should be 0 - 3 arguments.");
	}

})

base_object.@null@question = $varargs(function(args) {
	var first;
	if($typeof(args[0]) == $tfunction) {
		first = $exports.invoke(args[0], $array(), this);
	}
	else {
		first = args[0]
	}

	if($asize(args) == 0) {
		if(this == base_object.@null())
			base_object.@true();
		else
			base_object.@false();
	}
	else if($asize(args) == 1) {
		if(first == null || first == base_object.@null())
			base_object.@true();
		else 
			base_object.@false();
	}
	else if($asize(args) == 2) {
		if(first == null || first == base_object.@null()) {
			if($typeof(args[1]) == $tfunction) {
				$exports.invoke(args[1], $array(), this);
			}
			else {
				args[1];
			}
		}
		else {
			false;
		}
	}
	else if($asize(args) == 3) {
		if(first == null || first == base_object.@null()) {
			if($typeof(args[1]) == $tfunction) {
				$exports.invoke(args[1], $array(), this);
			}
			else {
				args[1];
			}
		}
		else {
			if($typeof(args[2]) == $tfunction) {
				$exports.invoke(args[2], $array(), this);
			}
			else {
				args[2];
			}

		}
	}
	else {
		$throw("Wrong number of arguments. Should be 0 - 3 arguments.");
	}
})

base_object.not = function(arg) {
	if(istrue(arg)) {
		base_object.@false();
	}
	else
	{
		base_object.@true();
	}
}


has_field = function(object, field_name) {
	if(object == null || $typeof(object) != $tobject) {
		return false;
	}
        if($objfield(object, $hash(field_name))) {
                return true;
        }
        else {
                var proto = $objgetproto(object);
                if(proto == null) {
                        return false;
                }
                else {
                        return has_field(proto, field_name);
                }
        }
}


base_array = new_brat(null, null);
base_array.@internal_array = $amake(0);

base_array.new = $varargs(function(args) {
	var ary = new_brat(base_array, null);
	ary.@internal_array = $acopy(args);
	ary.length = function() { $asize(ary.@internal_array) };
	ary;
});

base_array.get = $varargs(function(args) {
	if($asize(this.@internal_array) == 0) {
		return @base_object.@null();
	}
	if($asize(args) == 0) {
		throw("Invalid array access: null");
	}
	if($asize(args) == 1) {
		if(args[0] >= this.length()) {
			base_object.@null();
		}
		else {
			var res = this.@internal_array[args[0].@value];
			if(res == null)
				base_object.@null();
			else
				res;
		}
	}
	else if($asize(args) == 2) {
		var len = $asize(this.@internal_array);
		var start = args[0].@value;
		var stop = args[1].@value;
		if(start < 0) {
			start = len + start;
		}
		if(stop < 0) {
			stop = len + stop;
		}
		if(stop >= len) {
			stop = len - 1;
		}
		if(start >= len) {
			start = len -1;
		}
		if(start < stop) {
			$exports.make_array($asub(this.@internal_array, start, (stop - start + 1)));
		}
		else {
			$exports.make_array($asub(this.@internal_array, stop, (start - stop + 1)));
		}
	}
	else {
		$throw("Wrong number of arguments. Should be 1 or 2 arguments.");
	}

});

base_array.set = function(index, value) {
	index = index.@value;
	if(index >= this.length()) {
		var new_array = $amake(index - this.length() + 1);
		this.@internal_array = $aconcat($array(this.@internal_array, new_array));
		this.length = function() { index + 1 };
	}
	this.@internal_array[index] = value;
	this;
}

base_array.to@unders = function() {
	var i = 0;
	var len = $asize(this.@internal_array);
	var temp = $amake(len);
	while i < len {
		if(has_field(this.@internal_array[i], "to@unders"))
			temp[i] = this.@internal_array[i].to@unders();
		else if(hasfield(this.@internal_array[i], "@value"))
			temp[i] = this.@internal_array[i].@value;
		else
			temp[i] = this.@internal_array[i];

		i += 1;
	}
	temp;
}

base_array.__set = base_array.set;
base_array.__get = base_array.get;

base_hash = new_brat(null, null);
base_hash.@internal_hash = $hnew(0);

base_hash.new = $varargs(function(args) {
	var hsh = new_brat(base_hash, null);
	if($asize(args) == 0) {
		hsh.@internal_hash = $hnew(1);
	}
	else if($typeof(args[0]) == $tabstract) {
		hsh.@internal_hash = args[0];
	}
	else {
		throw("Invalid hash initialization:" + $string(args));
	}
	hsh.length = function() { $hcount(this.@internal_hash); };
	hsh;
});

base_hash.get = function(index) {
	if(has_field(index, "@value"))
		index = index.@value;
	if($hmem(this.@internal_hash, index, null))
		$hget(this.@internal_hash, index, null);
	else
		base_object.@null();
}

base_hash.set = function(index, value) {
	if(has_field(index, "@value"))
		index = index.@value;
	$hset(this.@internal_hash, index, value, null);
	this;
}

base_hash.__set = base_hash.set;
base_hash.__get = base_hash.get;

$exports.make_hash = function(neko_hash) {
	var hsh = base_hash.new();
	hsh.@internal_hash = neko_hash;
	hsh;
}

$exports.hset = function(hashtable, index, value) {
	if(has_field(index, "@value"))
		index = index.@value;
	$hset(hashtable, index, value, null)
}

$exports.make_array = function(neko_array) {
	var ary = base_array.new();
	ary.@internal_array = neko_array;
	ary.length = function() { $asize(ary.@internal_array) };
	ary;
}

base_number = new_brat(null, null);
base_number.@value = 0;
base_number.to@unders = function() {
	$string(this.@value);
}

base_number.new = function(num) {
	var n =  new_brat(base_number, null);
	if($typeof(num) == $tobject)
		n.@value = num.@value;
	else
		n.@value = num;
	n;
}

base_number.@plus = function(num) {
	var lhs = this.@value;
	var rhs = num.@value;

	if($typeof(lhs) == $tint && $typeof(rhs) == $tint)
		base_number.new($iadd(lhs, rhs));
	else
		base_number.new(lhs + rhs);
}

base_number.@minus = function(num) {
	var lhs = this.@value;
	var rhs = num.@value;

	if($typeof(lhs) == $tint && $typeof(rhs) == $tint)
		base_number.new($isub(lhs, rhs));
	else
		base_number.new(lhs - rhs);
}

base_number.@forward = function(num) {
	var lhs = this.@value;
	var rhs = num.@value;

	base_number.new(lhs / rhs);
}

base_number.@star = function(num) {
	var lhs = this.@value;
	var rhs = num.@value;

	if($typeof(lhs) == $tint && $typeof(rhs) == $tint)
		base_number.new($imult(lhs, rhs));
	else
		base_number.new(lhs * rhs);
}

base_number.@greater = function(num) {
	var lhs = this.@value;
	var rhs = num.@value;
	if(lhs > rhs)
		base_object.@true();
	else
		base_object.@false();
}

base_number.@less = function(num) {
	var lhs = this.@value;
	var rhs = num.@value;
	if(lhs < rhs)
		base_object.@true();
	else
		base_object.@false();
}

base_number.@less@equal = function(num) {
	var lhs = this.@value;
	var rhs = num.@value;
	if(lhs <= rhs)
		base_object.@true();
	else
		base_object.@false();
}

base_number.@greater@equal = function(num) {
	var lhs = this.@value;
	var rhs = num.@value;
	if(lhs >= rhs)
		base_object.@true();
	else
		base_object.@false();
}

base_string = new_brat(null, null);
base_string.@value = "";
base_string.to@unders = function() {
	this.@value;
}
base_string.new = $varargs(function(args) {
	var s = new_brat(base_string, null);
	if($asize(args) == 0) {
		s.@value = "";
	}
	else if($asize(args) == 1) {
		s.@value = $string(args[0]);
	}
	else {
		$throw("Too many arguments to string.new(). Only takes 0 or 1.");
	}
	s;
})

$exports.call_method = function(object, method, args) {
	//$print("Calling ", method, " on ", object, " with (", args, ")\n");
	if(object == null) {
		$throw("Method invoked on null object.");
	}
	else {
		$objcall(object, $hash(method), args);
	}
}

$exports.invoke = function(method, args, context) {
	//$print("Calling ", method, " with (", args, ")\n");
	if(method == null) {
		$throw("Could not invoke null method.");
	}
	else {
		$call(method, context, args);
	}
}

$exports.get_value = function(object, object_name, arguments, context) {
	if($typeof(object) == $tnull) {
		if($exports.has_field(context, object_name)) {
	 		$exports.call_method(context, object_name, arguments);
		}
		else
		{
			$throw("Trying to invoke null method:" + object_name + "\n");
		}
	} else {
		if($typeof(object) == $tfunction) {
			$exports.invoke(object, arguments, context);
		}
		else { 
			object; 
		}
	}
}

$exports.base_object = base_object;
$exports.has_field = has_field;
$exports.base_array = base_array;
$exports.base_hash = base_hash;
$exports.base_number = base_number;
$exports.base_string = base_string;
