includes 'parser/walker' 'parser/sexp' 'parser/env'

compiler = walker.new

compiler.init = { ast |
  my.ast = ast
  my.env = env.new
}

compiler.prototype.compile = {
  "#{file_header}
  _exports = {}
  local _main = function()
    #{method_header}

    setfenv(1, {})

    #{inner_brat}
  end

  local _result = coxpcall(_main, exception._handler)
  if not _lib then
    if not _result then
      os.exit(-1)
    else
      os.exit(0)
    end
  end
  "
}

compiler.file_header = "
  require \"coxpcall\"
  local _lib
  if package.loaded.core then
    _lib = true
  else
    _lib = false
    require \"core\"
  end
  "

compiler.method_header = "
  local object = object
  local array = array
  local number = number
  local string = base_string
  local exception = exception
  local hash = hash
  local regex = regex
  local _self = object
  local _type = type
  local _error = error
  local _tostring = tostring
  local brat_function = brat_function
  local _lifted_call = _lifted_call
  local _rawget = rawget
  "


compiler.prototype.r = { result_var, output, extras = null |
  o = object.new
  o.var = result_var
  o.output = output
  o.extras = extras
  o
}

compiler.walk :program { node |
  c = my
  results = node.nodes.map({ n |
    true? sexp?(n)
      { c.process n }
      { "" }
  })

  results.map(:output).join("\n")
}

compiler.walk :local_var_assign { node |
  rhs = process node.rhs
  temp = my.env.new_var node.lhs
  r temp, "local #{temp} = #{rhs.var}"
}

compiler.walk :number { node |
  val = node.value.to_s
  r val, val
}

export compiler, :compiler
