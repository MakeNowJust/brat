program = line+ <program>
        | eof

line = comment
     | space? exp:expression space? comment? (eof | eol | eob) <simple_exp>
     | empty_line

empty_line = space? eol

expression = regex | binary_operation | assignment | paren_exp | unary_operation | method_invocation | number | string
           | function_definition | array | hash 

paren_exp  = "(" space? exp:expression space? ")" <simple_exp>

assignment = field_access space "=" spaceorbreak !function_literal_invocation (function_definition | method_access)
           | field_access space "=" spaceorbreak expression
           | identifier space "=" spaceorbreak expression

field_access = method_chain (identifier | operator)

method_access = "->" (identifier | operator)
              | method_invocation "->" (identifier | operator)
              | paren_exp "->" (identifier | operator)
              | "->" paren_exp

#-- Basic literals

number  = "-"? /[0-9]+/ ("." /[0-9]+/)? <number>

array   = "[" spaceorbreak array_inner spaceorbreak "]"
        | "[" spaceorbreak "]"

array_inner = expression (((spaceorbreak "," spaceorbreak) | (space | eol)+) expression)+

hash    = "[" space? ":" space? "]"
        | "[" spaceorbreak hash_inner spaceorbreak "]"

hash_inner = hash_argument ((spaceorbreak "," spaceorbreak) | ((space | eol)+) hash_argument)*

hash_argument = (identifier | operator) (identifier | operator | number)* ":" spaceorbreak expression
              | expression spaceorbreak ":" spaceorbreak expression

regex   = "/" ("\\/" | !"/" . )* "/" /[mix]*/

string  = "\"" (string_interpolation | "\\\"" | !"\"" .)* "\""
        | "'" (!"'" .)* "'"
        | ":" (identifier | operator | number)+

string_interpolation = "#" "{" space? expression? space? (eol space? expression space?)* spaceorbreak "}"

comment = space? "#*" (!"*#" (comment | .))* "*#"
        | space? "#" (!(eol | eof) .)*

#-- Function definitions

function_definition = "{" space? formal_args space? line* space? "}"

formal_args = space? plain_formals space? "|" !"|"
            | space? default_args space? "|" !"|"
            | space? variable_args space? "|" !"|"
            | space? plain_formals space? "," space? variable_args space? "|" !"|" 
            | space? default_args space? "," space? variable_args space? "|" !"|"
            | space? plain_formals space? "," space? default_args space? "," space? variable_args space? "|" !"|"
            | space? plain_formals space? "," space? default_args space? "|" !"|"
            | !"|"

plain_formals = identifier rest_formals

rest_formals = (space? "," space? identifier !(space "="))*

default_args = default_arg (space? "," space? default_args)*

default_arg = identifier space "=" space default_arg_rhs_expression

default_arg_rhs_expression = paren_exp | method_invocation | number | string | function_definition | array | hash

variable_args = "*" identifier

#-- Unary and binary operations

unary_operation = !("-" number) operator !space unary_rhs_expression

unary_rhs_expression = paren_exp | number | string | array | hash | method_invocation | regex

binary_operation = binary_operation_chain expression

binary_operation_chain = (binary_lhs_expression space operator ((space? eol space?) | space))+

binary_lhs_expression = paren_exp | number | string | array | hash | regex | unary_operation | method_invocation

#-- Basic building blocks

identifier = /[a-zA-Z]/ (!"->" /[a-zA-Z0-9_!?\-*+^&@~\/\\><$%]/)*

operator = !("->" !operator) !("=" !(operator | "=")) ("!=" | ">=" | "<=" | /[!?\-*+^@~\/\\><$_%\=]/ | "||" | "|" | "&&" | "&")+

#-- Calling methods and functions

method_invocation = method_chain (identifier | operator) arg_list
                  | identifier arg_list
                  | paren_exp simple_arg_list
                  | function_literal_invocation

function_literal_invocation = function_definition !space simple_arg_list

method_chain = (method_lhs ".")+

method_lhs = identifier !space arg_list ("[" space? inner_arg_list space? "]")*
           | array ("[" space? inner_arg_list space? "]")
           | function_definition !space simple_arg_list
           | function_definition &"."
           | method_target_expression

method_target_expression = array | hash | paren_exp | number | string | regex | unary_operation

#-- Argument lists

arg_list = "(" space? inner_arg_list space? ")"
         | "(" space? ")"
         | space inner_arg_list
         | !"(" &(space?)

simple_arg_list = "(" space? inner_arg_list space? ")"
                | "(" space? ")"

inner_arg_list = arg_first arg_next*

arg_first = named_argument | expression

arg_space = (space | eol_not_semicolon)*


arg_next = arg_space "," arg_space arg_first
         | arg_space function_definition
         | arg_space named_argument
         | space? expression

named_argument = (identifier | operator) (identifier | operator | number)* ":" spaceorbreak expression
               | expression space? ":" space spaceorbreak expression

#-- Spaces

spaceorbreak = space? eol? space?

eol_not_semicolon = ("\n" | "\r\n")+

space   = (" " | "\t")+

eol     = ("\n" | ";" | "\r\n")+

eob     = space? &"}" space?

eof     = (eol | space)? !.
