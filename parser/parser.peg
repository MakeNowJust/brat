program = line+ <program>
        | eof <program>

line = comment
     | space? exp:expression space? comment? (eof | eol | eob) <simple_exp>
     | empty_line

empty_line = space? eol

expression = regex | binary_operation
           | index_set | index_get | assignment
           | paren_exp !(/\.|\(|->/) <simple_exp>
           | unary_operation
           | method_access | method_invocation | number | string
           | function_definition | array | hash

paren_exp  = "(" space? exp:expression space? ")" <simple_exp>

assignment = field_access space "=" spaceorbreak !function_literal_invocation (function_definition | method_access) <field_assign>
           | field_access space "=" spaceorbreak expression <field_assign>
           | var:identifier space "=" spaceorbreak expression <var_assign>

field_access = method_chain (identifier | operator) <field_access>

index_set = indexed_expression sindexes:("[" expression "]")+ space? "=" spaceorbreak expression <index_set>

index_get = indexed_expression gindexes:("[" inner_arg_list "]")+ !"." iargs:simple_arg_list? <index_get>

indexed_expression = array | hash | paren_exp | unary_operation | method_invocation | string

method_access = "->" meth:(identifier | operator) !"." <simple_meth_access>
              | target:method_invocation "->" meth:(identifier | operator) <meth_access>
              | paren_exp "->" meth:(identifier | operator) <paren_meth_access>
              | "->" paren_exp <simple_paren_meth_access>

#-- Basic literals

number  = /-?[0-9]+(\.[0-9]+)?/ <bnumber>

array   = "[" spaceorbreak inner:array_inner spaceorbreak "]" <barray>
        | "[" spaceorbreak "]" <empty_array>

array_inner = first:expression rest:(((spaceorbreak "," spaceorbreak) | (space | eol)+) expression)* <array_inner>

hash    = "[" space? ":" space? "]" <empty_hash>
        | "[" spaceorbreak inner:hash_inner spaceorbreak "]" <bhash>

hash_inner = first:hash_argument rest:(((spaceorbreak "," spaceorbreak) | ((space | eol)+)) hash_argument)* <array_inner>

hash_argument = key:((identifier | operator) (identifier | operator | number)*) ":" spaceorbreak value:expression <hash_key_arg>
              | key:expression spaceorbreak ":" spaceorbreak value:expression <hash_arg>

regex   = "/" body:("\\/" | !"/" . )* "/" opts:/[mix]*/ <bregex>

string  = ("''" | "\"\"") <empty_string>
        | "\"" values:(/[^#"\\]+/ | string_interpolation | "\\\"" | !"\"" .)* "\"" <string_interp>
        | "'" svalue:("\\'" | !"'" .)+ "'" <simple_string>
        | ":" svalue:(identifier | operator | number)+ <simple_string>

string_interpolation = "#" "{" space? first:expression? space? rest:(eol space? expression space?)* spaceorbreak "}" <interp_value>

comment = space? "#*" (!"*#" (comment | .))* ("*#" | eof)
        | space? "#" (!(eol | eof) .)*

#-- Function definitions

function_definition = "{" space? args:formal_args? space? body:line* space? "}" <bfunction>

formal_args = space? plain_formals space? "|" !"|"
            | space? default_args space? "|" !"|"
            | space? variable_args space? "|" !"|"
            | space? plain_formals space? "," space? variable_args space? "|" !"|"
            | space? default_args space? "," space? variable_args space? "|" !"|"
            | space? plain_formals space? "," space? default_args space? "," space? variable_args space? "|" !"|"
            | space? plain_formals space? "," space? default_args space? "|" !"|"
            | "|"

plain_formals = plain_arg rest:rest_formals

rest_formals = (space? "," space? plain_arg !(space "="))*

plain_arg = identifier <plain_arg>

default_args = default_arg (space? "," space? default_args)*

default_arg = arg_var:identifier space "=" space arg_val:default_arg_rhs_expression <default_arg>

default_arg_rhs_expression = index_get | paren_exp | method_invocation | number | string | function_definition | array | hash

variable_args = "*" arg_var:identifier <variable_args>

#-- Unary and binary operations

unary_operation = !("-" number) operator !space unary_rhs_expression <unary_op>

unary_rhs_expression = index_get | paren_exp | number | string | array | hash | method_invocation | regex

binary_operation = binary_operation_chain expression <binary_op>

binary_operation_chain = (binary_lhs_expression space operator ((space? eol space?) | space))+ <binary_op_chain>

binary_lhs_expression = simple_method_chain | index_get | paren_exp | string | array | hash | regex | unary_operation | number | method_invocation

#-- Basic building blocks

identifier = /[a-zA-Z](?:(?!->)[a-zA-Z0-9_!?\-*+^&@~\/\\><$%])*/

operator = !("->" !operator) !("=" !(operator | "=")) ("!=" | ">=" | "<=" | /[!?\-*+^@~\/\\><$_%\=]/ | "||" | "|" | "&&" | "&")+

#-- Calling methods and functions

method_invocation = method_chain (identifier | operator) arg_list <chain_call>
                  | simple_method_invocation
                  | m_name:identifier alist:arg_list <simple_call>
                  | paren_exp simple_arg_list <paren_call>
                  | function_literal_invocation

simple_method_invocation =  m_name:identifier alist:simple_arg_list <simpler_call>

simple_method_chain = method_chain (identifier | operator) simple_arg_list <chain_call>
                    | method_chain (identifier | operator) &space <chain_call>
                    | simple_method_invocation
                    | identifier &space <simplest_call>

function_literal_invocation = func:function_definition alist:simple_arg_list <func_lit_call>

method_chain = (method_lhs ".")+ <method_chain>

method_lhs = var:identifier !space args:arg_list ("[" space? index_args:inner_arg_list space? "]")* <simple_meth_lhs>
           | target:array ("[" space? index_args:inner_arg_list space? "]") <array_index_lhs>
           | function_definition !space simple_arg_list
           | function_definition &"."
           | "->" var:identifier <access_meth_lhs>
           | method_target_expression

method_target_expression = array | hash | paren_exp | number | string | regex | unary_operation

#-- Argument lists

arg_list = "(" space? inner_arg_list space? ")"
         | "(" space? ")"
         | space inner_arg_list
         | !"(" &(space?)

simple_arg_list = "(" space? inner_arg_list space? ")"
                | "(" space? ")"

inner_arg_list = arg_first arg_next* <inner_arg_list>

arg_first = named_argument | expression

arg_space = (space | eol_not_semicolon)

arg_next = arg_space* "," arg_space* arg_first
         | arg_space+ function_definition
         | arg_space+ named_argument
         | space? expression

named_argument = key:identifier ":" spaceorbreak value:expression <named_argument>
               | key:string ":" spaceorbreak value:expression <named_argument>
               | key:(method_target_expression | simple_method_invocation) space ":" space spaceorbreak value:expression <named_argument>
               | key:identifier space ":" spaceorbreak value:expression <named_argument_call>

#-- Spaces

spaceorbreak = space? eol? space?

eol_not_semicolon = ("\n" | "\r\n")

space   = /(?: |\t)+/

eol     = /(?:\n|;|\r\n)+/

eob     = space? &"}" space?

eof     = (eol | space)? !.
